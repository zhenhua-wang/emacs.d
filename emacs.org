#+Title: Emacs Configuration
#+AUTHOR: Zhenhua Wang
#+auto_tangle: t
#+PROPERTY: header-args+ :tangle "yes"

This is my personal configuration for emacs. I am using ~(org-babel-load-file)~ to directly load it into init.el. Since this function will only tangle the file only if the current file is new, I am using ~if-else/when/pcase~ to detect OS.

* Startup
** Add custom file and path

#+begin_src emacs-lisp
;; Load path for manually installed packages
(add-to-list 'load-path (expand-file-name "lisp" user-emacs-directory))

;; Load path for customied themes
(add-to-list 'custom-theme-load-path (expand-file-name "themes" user-emacs-directory))
#+end_src

** Keep .emacs.d Clean
Again, steal from [[https://github.com/daviwil/dotfiles/blob/master/Emacs.org][Daviwil's config]].

#+begin_src emacs-lisp
;; Change the user-emacs-directory to keep unwanted things out of ~/.emacs.d
(setq user-emacs-directory (expand-file-name "~/.cache/emacs/")
      url-history-file (expand-file-name "url/history" user-emacs-directory))

;; Use no-littering to automatically set common paths to the new user-emacs-directory
(use-package no-littering)

;; Keep customization settings in a temporary file (thanks Ambrevar!)
(setq custom-file
      (if (boundp 'server-socket-dir)
          (expand-file-name "custom.el" server-socket-dir)
          (expand-file-name (format "emacs-custom-%s.el" (user-uid)) temporary-file-directory)))
(load custom-file t)
#+end_src

** Measuring start time

Source: [[https://github.com/daviwil/dotfiles/blob/master/Emacs.org][Daviwil's config]] and [[https://github.com/purcell/emacs.d/blob/master/lisp/init-benchmarking.el][purcell's benchmarking]]

#+begin_src emacs-lisp
;; The default is 800 kilobytes.  Measured in bytes.
(setq gc-cons-threshold (* 50 1000 1000))

;; (require 'init-benchmarking)
#+end_src

** Package management
*** package and use-packge
To update packages, run ~list-packages~ then hit U to mark available upgrades, then x to upgrade.

+ ~:hook~ - package will be loaded the first time one of the hooks is invoked
+ ~:bind~ - package will be loaded the first time one of the key bindings if used
+ ~:commands~ - package will be loaded when one of the commands are used
+ ~:mode~ - package will be loaded the first time a file with a particular extension is opened
+ ~:after~ - loaded after other specific packages are loaded
+ ~:defer~ - if no other options apply, defer loading after startup
+ ~:command~ - must load at init

#+begin_src emacs-lisp
(require 'package)
(setq package-archives '(("melpa" . "https://melpa.org/packages/")
                         ("melpa-stable" . "https://stable.melpa.org/packages/")
                         ("org" . "https://orgmode.org/elpa/")
                         ("elpa" . "https://elpa.gnu.org/packages/")))

(package-initialize)

(unless package-archive-contents
  (package-refresh-contents))

  ;; Initialize use-package on non-Linux platforms
(unless (package-installed-p 'use-package)
  (package-install 'use-package))

(setq use-package-always-ensure t)
(setq use-package-verbose t)

(require 'use-package)
#+end_src
*** straight
#+begin_src emacs-lisp
;; Bootstrap straight.el
(defvar bootstrap-version)
(let ((bootstrap-file
      (expand-file-name "straight/repos/straight.el/bootstrap.el" user-emacs-directory))
      (bootstrap-version 5))
  (unless (file-exists-p bootstrap-file)
    (with-current-buffer
        (url-retrieve-synchronously
        "https://raw.githubusercontent.com/raxod502/straight.el/develop/install.el"
        'silent 'inhibit-cookies)
      (goto-char (point-max))
      (eval-print-last-sexp)))
  (load bootstrap-file nil 'nomessage))

;; Always use straight to install on systems other than Linux
(setq straight-use-package-by-default (not (eq system-type 'gnu/linux)))

;; Use straight.el for use-package expressions
(straight-use-package 'use-package)

;; Load the helper package for commands like `straight-x-clean-unused-repos'
(require 'straight-x)
#+end_src

#+RESULTS:
: straight-x

** Server dode
Start the Emacs server from this instance so that all emacsclient calls are routed here.

#+begin_src emacs-lisp
(server-start)
#+end_src

* UI setting
** Theme
#+begin_src emacs-lisp

(use-package spacegray-theme)
(use-package doom-themes)
(use-package gruvbox-theme)
(use-package nord-theme)

;; (load-theme 'doom-oceanic-next t)
;; (load-theme 'doom-wilmersdorf t)
;; (load-theme 'doom-nord t)
;; (load-theme 'doom-spacegrey t)

(pcase system-type
  ('gnu/linux (load-theme 'doom-wilmersdorf t))
    ('darwin (load-theme 'doom-spacegrey t)))

;; hightlight current row
(global-hl-line-mode t)

;; apply a beacon effect to the hightlighted line
(use-package beacon
  :disabled
  :config
  ;; (setq beacon-blink-when-window-scrolls nil)
  (beacon-mode))
#+end_src

** Font face

Vanilla Emacs: You can use ~C-x C-+~ and ~C-x C--~ (~text-scale-adjust~) to increase or decrease the buffer text size (~C-+~ or ~C--~ to repeat).

#+begin_src emacs-lisp
(pcase system-type
  ((or 'gnu/linux 'windows-nt 'cygwin)
   (setq zw/font-size 140))
  ('darwin
   (setq zw/font-size 180)))

;; set the default face
(set-face-attribute 'default nil
                       :font "JetBrainsMono Nerd Font"
                       ;; :background "black"
                       ;; make fonts less tranparent
                       :foreground "white"
                       :weight 'medium
                       :height zw/font-size)

;; Set the fixed pitch face
(set-face-attribute 'fixed-pitch nil
                    :font "JetBrainsMono Nerd Font"
                    ;; :background "black"
                    :foreground "white"
                    :weight 'normal
                    :height zw/font-size)

;; Set the variable pitch face
(set-face-attribute 'variable-pitch nil
                    :font "Iosevka Aile" ;"Cantarell"
                    ;; :background "black"
                    :foreground "white"
                    :weight 'light
                    :height zw/font-size)
#+end_src

** line numbers mode
#+begin_src emacs-lisp
(column-number-mode)
(add-hook 'prog-mode-hook 'display-line-numbers-mode)
(add-hook 'text-mode-hook 'display-line-numbers-mode)
(add-hook 'conf-mode-hook 'display-line-numbers-mode)


;; Override some modes which derive from the above
(dolist (mode '(org-mode-hook))
  (add-hook mode (lambda () (display-line-numbers-mode 0))))
#+end_src

** Doom modeline
#+begin_src emacs-lisp
(use-package minions
  :hook (doom-modeline-mode . minions-mode))

(use-package doom-modeline
  :hook (after-init . doom-modeline-init)
  :custom-face
  (mode-line ((t (:height 0.5))))
  (mode-line-inactive ((t (:height 0.5))))
  :custom
  (doom-modeline-height 10)
  (doom-modeline-bar-width 6)
  (doom-modeline-lsp t)
  (doom-modeline-github t)
  (doom-modeline-mu4e nil)
  (doom-modeline-irc t)
  (doom-modeline-minor-modes nil)
  (doom-modeline-persp-name nil)
  (doom-modeline-buffer-file-name-style 'truncate-except-project)
  (doom-modeline-major-mode-icon t)
  :config
  (doom-modeline-mode 1)
  (setq neo-theme (if (display-graphic-p) 'icons 'arrow)))
#+end_src

** Rain-bow-delimiters
#+begin_src emacs-lisp
(use-package rainbow-delimiters
  :config
  (add-hook 'prog-mode-hook #'rainbow-delimiters-mode))
#+end_src

** Highlight Matching Braces
#+begin_src emacs-lisp
(use-package paren
  :config
  (set-face-attribute 'show-paren-match-expression nil :background "#363e4a")
  (set-face-attribute 'show-paren-match nil :weight 'extra-bold)
  (set-face-foreground 'show-paren-match "#BF616A") ;; set matched color red
  (show-paren-mode 1))
#+end_src

** Use spaces instead of tabs for indentation
#+begin_src emacs-lisp
(setq-default indent-tabs-mode nil)
#+end_src

** toggle transparency
#+begin_src emacs-lisp
(defun zw/toggle-transparency ()
  (interactive)
  (let ((alpha (frame-parameter nil 'alpha)))
    (set-frame-parameter
     nil 'alpha
     (if (eql (cond ((numberp alpha) alpha)
                    ((numberp (cdr alpha)) (cdr alpha))
                    ;; Also handle undocumented (<active> <inactive>) form.
                    ((numberp (cadr alpha)) (cadr alpha)))
              100)
         '(85 . 85) '(100 . 100)))))
#+end_src

** good scroll mode
#+begin_src emacs-lisp
(use-package good-scroll
  :disabled
  :init
  (good-scroll-mode 1))
#+end_src

** Other packages
#+begin_src emacs-lisp
(use-package command-log-mode
  :commands command-log-mode)
(use-package neotree
  :commands neotree)
#+end_src

* System utilities
** Exec path from shell
#+begin_src emacs-lisp
(use-package exec-path-from-shell
  :init
  (setq exec-path-from-shell-check-startup-files nil)
  :config
  (when (memq window-system '(mac ns x))
    (exec-path-from-shell-initialize)))
#+end_src

** TRAMP
For host with two factor auth, you need to
1. enter password
2. enter the second-step code

#+begin_src emacs-lisp
;; Set default connection mode to SSH
(setq tramp-default-method "ssh")
#+end_src

** Input method

#+begin_src emacs-lisp
  (use-package rime
    :custom
    (default-input-method "rime")
    :config
    (setq rime-user-data-dir "~/.config/rime")
    (setq rime-show-candidate 'sidewindow)
    ;; (setq rime-show-candidate 'minibuffer)
    (setq rime-translate-keybindings
          '("C-f" "C-b" "C-n" "C-p" "C-g" "<left>" "<right>" "<up>" "<down>" "<prior>" "<next>" "<delete>")))
#+end_src

** Open with external apps

use =feh= to open images, use =evince= to open pdf

#+begin_src emacs-lisp
(use-package openwith
  :if (eq system-type 'gnu/linux)
  :config
  (setq openwith-associations
        (list
         ;; (list (openwith-make-extension-regexp
         ;;        '("xbm" "pbm" "pgm" "ppm" "pnm"
         ;;          "png" "gif" "bmp" "tif" "jpeg" "jpg"))
         ;;       "feh"
         ;;       '(file))
         (list (openwith-make-extension-regexp
                '("pdf"))
               ;; "zathura"
               "okular"
               ;; "evince"
               '(file))
         ;;I promise I will get rid of this someday..
         (list (openwith-make-extension-regexp
                '("doc" "docx" "ppt" "pptx"))
               "libreoffice"
               '(file))))
  (openwith-mode 1))
#+end_src

* Productivity
** Company
*** Main
#+begin_src emacs-lisp
(use-package company
  ;; :hook (after-init . global-company-mode)
  :hook ((prog-mode . company-mode)
         (LaTeX-mode . company-mode)
         (latex-mode . company-mode)
         (markdown-mode . company-mode)
         (org-mode . company-mode))
  ;; :if (eq system-type 'gnu/linux)
  :bind
  (:map company-mode-map
         ("M-<tab>" . company-yasnippet))
  (:map company-active-map
        ("<tab>" . company-complete-selection)
        ("M-<tab>" . company-yasnippet))
  :custom
  (company-idle-delay 0)
  (company-show-numbers t)
  (completion-ignore-case t)
  (company-dabbrev-downcase nil)
  (company-require-match 'never)
  (company-dabbrev-ignore-case nil)
  (company-selection-wrap-around t)
  (company-minimum-prefix-length 1)
  (company-tooltip-align-annotations t)
  (company-backends '((company-capf :with company-yasnippet)
                           (company-dabbrev-code company-keywords company-files)
                           company-dabbrev)))

(use-package company-fuzzy
  :disabled
  :config
  (global-company-fuzzy-mode 1))

(use-package company-prescient
  :after company
  ;; :if (eq system-type 'gnu/linux)
  :config
  (company-prescient-mode 1))
#+end_src
*** Company tabnine
#+begin_src emacs-lisp
(use-package company-tabnine
  :disabled
  :defer 1
  :custom
  (company-tabnine-max-num-results 9)
  :config
  (add-to-list 'company-backends #'company-tabnine))
#+end_src
*** Company math
#+begin_src emacs-lisp
(use-package company-math
  :init
  (setq company-math-allow-latex-symbols-in-faces  t)
  :config
  (defun org-latex-setup ()
    (setq-local company-backends
                (append '((company-math-symbols-latex company-latex-commands))
                        company-backends)))
  (add-hook 'org-mode-hook 'org-latex-setup))
#+end_src
*** Company box
#+begin_src emacs-lisp
(use-package company-box
  :diminish
  :if (display-graphic-p)
  :defines company-box-icons-all-the-icons
  :hook (company-mode . company-box-mode)
  :custom
  ;; (company-box-backends-colors nil)
  (company-box-doc-delay 0.1)
  (company-box-doc-frame-parameters '((internal-border-width . 1)
                                      (left-fringe . 3)
                                      (right-fringe . 3))))

(use-package company-box
  :disabled
  :diminish
  :if (display-graphic-p)
  :defines company-box-icons-all-the-icons
  :hook (company-mode . company-box-mode)
  :custom
  ;; (company-box-backends-colors nil)
  (company-box-doc-delay 0.1)
  (company-box-doc-frame-parameters '((internal-border-width . 1)
                                      (left-fringe . 3)
                                      (right-fringe . 3)))
  :config
  (with-no-warnings
    ;; Prettify icons
    (defun my-company-box-icons--elisp (candidate)
      (when (or (derived-mode-p 'emacs-lisp-mode) (derived-mode-p 'lisp-mode))
        (let ((sym (intern candidate)))
          (cond ((fboundp sym) 'Function)
                ((featurep sym) 'Module)
                ((facep sym) 'Color)
                ((boundp sym) 'Variable)
                ((symbolp sym) 'Text)
                (t . nil)))))
    (advice-add #'company-box-icons--elisp :override #'my-company-box-icons--elisp)

    ;; Credits to Centaur for these configurations
    ;; Display borders and optimize performance
    (defun my-company-box--display (string on-update)
      "Display the completions."
      (company-box--render-buffer string on-update)

      (let ((frame (company-box--get-frame))
            (border-color (face-foreground 'font-lock-comment-face nil t)))
        (unless frame
          (setq frame (company-box--make-frame))
          (company-box--set-frame frame))
        (company-box--compute-frame-position frame)
        (company-box--move-selection t)
        (company-box--update-frame-position frame)
        (unless (frame-visible-p frame)
          (make-frame-visible frame))
        (company-box--update-scrollbar frame t)
        (set-face-background 'internal-border border-color frame)
        (when (facep 'child-frame-border)
          (set-face-background 'child-frame-border border-color frame)))
      (with-current-buffer (company-box--get-buffer)
        (company-box--maybe-move-number (or company-box--last-start 1))))
    (advice-add #'company-box--display :override #'my-company-box--display)

    (defun my-company-box-doc--make-buffer (object)
      (let* ((buffer-list-update-hook nil)
             (inhibit-modification-hooks t)
             (string (cond ((stringp object) object)
                           ((bufferp object) (with-current-buffer object (buffer-string))))))
        (when (and string (> (length (string-trim string)) 0))
          (with-current-buffer (company-box--get-buffer "doc")
            (erase-buffer)
            (insert (propertize "\n" 'face '(:height 0.5)))
            (insert string)
            (insert (propertize "\n\n" 'face '(:height 0.5)))

            ;; Handle hr lines of markdown
            ;; @see `lsp-ui-doc--handle-hr-lines'
            (with-current-buffer (company-box--get-buffer "doc")
              (let (bolp next before after)
                (goto-char 1)
                (while (setq next (next-single-property-change (or next 1) 'markdown-hr))
                  (when (get-text-property next 'markdown-hr)
                    (goto-char next)
                    (setq bolp (bolp)
                          before (char-before))
                    (delete-region (point) (save-excursion (forward-visible-line 1) (point)))
                    (setq after (char-after (1+ (point))))
                    (insert
                     (concat
                      (and bolp (not (equal before ?\n)) (propertize "\n" 'face '(:height 0.5)))
                      (propertize "\n" 'face '(:height 0.5))
                      (propertize " "
                                  'display '(space :height (1))
                                  'company-box-doc--replace-hr t
                                  'face `(:background ,(face-foreground 'font-lock-comment-face)))
                      (propertize " " 'display '(space :height (1)))
                      (and (not (equal after ?\n)) (propertize " \n" 'face '(:height 0.5)))))))))

            (setq mode-line-format nil
                  display-line-numbers nil
                  header-line-format nil
                  show-trailing-whitespace nil
                  cursor-in-non-selected-windows nil)
            (current-buffer)))))
    (advice-add #'company-box-doc--make-buffer :override #'my-company-box-doc--make-buffer)

    ;; Display the border and fix the markdown header properties
    (defun my-company-box-doc--show (selection frame)
      (cl-letf (((symbol-function 'completing-read) #'company-box-completing-read)
                (window-configuration-change-hook nil)
                (inhibit-redisplay t)
                (display-buffer-alist nil)
                (buffer-list-update-hook nil))
        (-when-let* ((valid-state (and (eq (selected-frame) frame)
                                       company-box--bottom
                                       company-selection
                                       (company-box--get-frame)
                                       (frame-visible-p (company-box--get-frame))))
                     (candidate (nth selection company-candidates))
                     (doc (or (company-call-backend 'quickhelp-string candidate)
                              (company-box-doc--fetch-doc-buffer candidate)))
                     (doc (company-box-doc--make-buffer doc)))
          (let ((frame (frame-local-getq company-box-doc-frame))
                (border-color (face-foreground 'font-lock-comment-face nil t)))
            (unless (frame-live-p frame)
              (setq frame (company-box-doc--make-frame doc))
              (frame-local-setq company-box-doc-frame frame))
            (set-face-background 'internal-border border-color frame)
            (when (facep 'child-frame-border)
              (set-face-background 'child-frame-border border-color frame))
            (company-box-doc--set-frame-position frame)

            ;; Fix hr props. @see `lsp-ui-doc--fix-hr-props'
            (with-current-buffer (company-box--get-buffer "doc")
              (let (next)
                (while (setq next (next-single-property-change (or next 1) 'company-box-doc--replace-hr))
                  (when (get-text-property next 'company-box-doc--replace-hr)
                    (put-text-property next (1+ next) 'display
                                       '(space :align-to (- right-fringe 1) :height (1)))
                    (put-text-property (1+ next) (+ next 2) 'display
                                       '(space :align-to right-fringe :height (1)))))))

            (unless (frame-visible-p frame)
              (make-frame-visible frame))))))
    (advice-add #'company-box-doc--show :override #'my-company-box-doc--show)

    (defun my-company-box-doc--set-frame-position (frame)
      (-let* ((frame-resize-pixelwise t)

              (box-frame (company-box--get-frame))
              (box-position (frame-position box-frame))
              (box-width (frame-pixel-width box-frame))
              (box-height (frame-pixel-height box-frame))
              (box-border-width (frame-border-width box-frame))

              (window (frame-root-window frame))
              ((text-width . text-height) (window-text-pixel-size window nil nil
                                                                  (/ (frame-pixel-width) 2)
                                                                  (/ (frame-pixel-height) 2)))
              (border-width (or (alist-get 'internal-border-width company-box-doc-frame-parameters) 0))

              (x (- (+ (car box-position) box-width) border-width))
              (space-right (- (frame-pixel-width) x))
              (space-left (car box-position))
              (fringe-left (or (alist-get 'left-fringe company-box-doc-frame-parameters) 0))
              (fringe-right (or (alist-get 'right-fringe company-box-doc-frame-parameters) 0))
              (width (+ text-width border-width fringe-left fringe-right))
              (x (if (> width space-right)
                     (if (> space-left width)
                         (- space-left width)
                       space-left)
                   x))
              (y (cdr box-position))
              (bottom (+ company-box--bottom (frame-border-width)))
              (height (+ text-height (* 2 border-width)))
              (y (cond ((= x space-left)
                        (if (> (+ y box-height height) bottom)
                            (+ (- y height) border-width)
                          (- (+ y box-height) border-width)))
                       ((> (+ y height) bottom)
                        (- (+ y box-height) height))
                       (t y))))
        (set-frame-position frame (max x 0) (max y 0))
        (set-frame-size frame text-width text-height t)))
    (advice-add #'company-box-doc--set-frame-position :override #'my-company-box-doc--set-frame-position))

  (when (require 'all-the-icons nil t)
    (declare-function all-the-icons-faicon 'all-the-icons)
    (declare-function all-the-icons-material 'all-the-icons)
    (declare-function all-the-icons-octicon 'all-the-icons)
    (setq company-box-icons-all-the-icons
          `((Unknown . ,(all-the-icons-material "find_in_page" :height 1.0 :v-adjust -0.2))
                        (Text . ,(all-the-icons-faicon "text-width" :height 1.0 :v-adjust -0.02))
                        (Method . ,(all-the-icons-faicon "cube" :height 1.0 :v-adjust -0.02 :face 'all-the-icons-purple))
                        (Function . ,(all-the-icons-faicon "cube" :height 1.0 :v-adjust -0.02 :face 'all-the-icons-purple))
                        (Constructor . ,(all-the-icons-faicon "cube" :height 1.0 :v-adjust -0.02 :face 'all-the-icons-purple))
                        (Field . ,(all-the-icons-octicon "tag" :height 1.1 :v-adjust 0 :face 'all-the-icons-lblue))
                        (Variable . ,(all-the-icons-octicon "tag" :height 1.1 :v-adjust 0 :face 'all-the-icons-lblue))
                        (Class . ,(all-the-icons-material "settings_input_component" :height 1.0 :v-adjust -0.2 :face 'all-the-icons-orange))
                        (Interface . ,(all-the-icons-material "share" :height 1.0 :v-adjust -0.2 :face 'all-the-icons-lblue))
                        (Module . ,(all-the-icons-material "view_module" :height 1.0 :v-adjust -0.2 :face 'all-the-icons-lblue))
                        (Property . ,(all-the-icons-faicon "wrench" :height 1.0 :v-adjust -0.02))
                        (Unit . ,(all-the-icons-material "settings_system_daydream" :height 1.0 :v-adjust -0.2))
                        (Value . ,(all-the-icons-material "format_align_right" :height 1.0 :v-adjust -0.2 :face 'all-the-icons-lblue))
                        (Enum . ,(all-the-icons-material "storage" :height 1.0 :v-adjust -0.2 :face 'all-the-icons-orange))
                        (Keyword . ,(all-the-icons-material "filter_center_focus" :height 1.0 :v-adjust -0.2))
                        (Snippet . ,(all-the-icons-material "format_align_center" :height 1.0 :v-adjust -0.2))
                        (Color . ,(all-the-icons-material "palette" :height 1.0 :v-adjust -0.2))
                        (File . ,(all-the-icons-faicon "file-o" :height 1.0 :v-adjust -0.02))
                        (Reference . ,(all-the-icons-material "collections_bookmark" :height 1.0 :v-adjust -0.2))
                        (Folder . ,(all-the-icons-faicon "folder-open" :height 1.0 :v-adjust -0.02))
                        (EnumMember . ,(all-the-icons-material "format_align_right" :height 1.0 :v-adjust -0.2))
                        (Constant . ,(all-the-icons-faicon "square-o" :height 1.0 :v-adjust -0.1))
                        (Struct . ,(all-the-icons-material "settings_input_component" :height 1.0 :v-adjust -0.2 :face 'all-the-icons-orange))
                        (Event . ,(all-the-icons-octicon "zap" :height 1.0 :v-adjust 0 :face 'all-the-icons-orange))
                        (Operator . ,(all-the-icons-material "control_point" :height 1.0 :v-adjust -0.2))
                        (TypeParameter . ,(all-the-icons-faicon "arrows" :height 1.0 :v-adjust -0.02))
                        (Template . ,(all-the-icons-material "format_align_left" :height 1.0 :v-adjust -0.2)))
          company-box-icons-alist 'company-box-icons-all-the-icons)))

#+end_src

#+RESULTS:
| company-box-mode | company-mode-set-explicitly |

** Counsel & ivy
*** main
**** ivy 
#+begin_src emacs-lisp
;; ivy
(use-package ivy
  :diminish
  :bind (("s-f" . swiper)
         ("C-s" . swiper)
         :map ivy-minibuffer-map
         ("TAB" . ivy-alt-done)
         ;; override s-tab from creating another minibuffer and make it behave mac-like
         ("s-<tab>" . ivy-next-line) ; "C-j"
         ;; ("s-SPC" . ivy-next-line)
         ("<backtab>" . ivy-previous-line))
  :config
  (ivy-mode 1)
  (setq ivy-use-virtual-buffers t)
  (setq ivy-wrap t)
  (setq ivy-count-format "(%d/%d) ")
  (setq enable-recursive-minibuffers t)
  (setq confirm-nonexistent-file-or-buffer t)

  ;; Set minibuffer height for different commands
  (setf (alist-get 'counsel-projectile-ag ivy-height-alist) 15)
  (setf (alist-get 'counsel-projectile-rg ivy-height-alist) 15)
  (setf (alist-get 'swiper ivy-height-alist) 15)
  (setf (alist-get 'counsel-switch-buffer ivy-height-alist) 7))
#+end_src

**** counsel
#+begin_src emacs-lisp
(use-package counsel
  :demand t
  :bind (("M-x" . counsel-M-x)
	 ("C-x b" . switch-to-buffer)
	 ("C-x C-f" . counsel-find-file)
	 ;; ("C-c b" . counsel-switch-buffer)
	 ("C-c i" . counsel-imenu)
         ("C-c s" . 'counsel-search)
	 :map minibuffer-local-map
	 ("C-r" . 'counsel-minibuffer-history))
  :custom
  (counsel-linux-app-format-function #'counsel-linux-app-format-function-name-only)
  :config
  (pcase system-type
    ('darwin
     (setq browse-url-browser-function 'xwidget-webkit-browse-url))
    ('gnu/linux
     (setq browse-url-browser-function 'browse-url-generic
      browse-url-generic-program "qutebrowser")))
  (setq counsel-search-engine 'google)
  (counsel-mode 1))
#+end_src

**** better search results
#+begin_src emacs-lisp
(use-package flx  ;; Improves sorting for fuzzy-matched results
  :after ivy
  :defer 1
  :init
  (setq ivy-flx-limit 10000))

;; precscient
(use-package ivy-prescient
  :after counsel
  :config
  (ivy-prescient-mode 1)
  :custom
  (setq ivy-prescient-enable-filtering t)
  )

(use-package prescient
  :after counsel
  :config
  (prescient-persist-mode 1)
  (setq prescient-sort-length-enable t)
  ;; (setq prescient-history-length 20)
  )
#+end_src

*** beautify counsel ivy
**** icon packages
#+begin_src emacs-lisp
(use-package all-the-icons-ivy
  :hook
  (after-init . all-the-icons-ivy-setup)
  :config
  (setq all-the-icons-ivy-file-commands
        '(counsel-find-file counsel-recentf counsel-ibuffer counsel-switch-buffer)))

(use-package all-the-icons-ivy-rich
  :init (all-the-icons-ivy-rich-mode 1)
  :config
  (setq all-the-icons-ivy-rich-color-icon t))
#+end_src

**** ivy-rich
#+begin_src emacs-lisp
(use-package ivy-rich
  :init
  (ivy-rich-mode 1)
  :after counsel
  :config
  (setcdr (assq t ivy-format-functions-alist) #'ivy-format-function-line)
  (setq ivy-format-function #'ivy-format-function-line)
  (setq ivy-rich-display-transformers-list
        (plist-put ivy-rich-display-transformers-list
                   'ivy-switch-buffer
                   '(:columns
                     ((ivy-rich-candidate (:width 40))
                      (ivy-rich-switch-buffer-indicators (:width 4 :face error :align right)); return the buffer indicators
                      (ivy-rich-switch-buffer-major-mode (:width 12 :face warning))          ; return the major mode info
                      (ivy-rich-switch-buffer-project (:width 15 :face success))             ; return project name using `projectile'
                      (ivy-rich-switch-buffer-path (:width (lambda (x) (ivy-rich-switch-buffer-shorten-path x (ivy-rich-minibuffer-width 0.3))))))  ; return file path relative to project root or `default-directory' if project is nil
                     :predicate
                     (lambda (cand)
                       (if-let ((buffer (get-buffer cand)))
                           ;; Don't mess with EXWM buffers
                           (with-current-buffer buffer
                             (not (derived-mode-p 'exwm-mode)))))))))
#+end_src

** Projectile
+ Supported Project Types
  - Directories that contain the special .projectile file

  - Directories under version control (e.g. a Git repo)

  - Directories that contain some project description file (e.g. a Gemfile for Ruby projects or pom.xml for Java maven-based projects)
#+begin_src emacs-lisp
(use-package projectile
  :config (projectile-mode +1)
  :demand t
  :bind-keymap
  ("C-c p" . projectile-command-map)
  :init
  (when (file-directory-p "~/Workspace/Documents/Graduate/Mizzou")
    (setq projectile-project-search-path '("~/Workspace/Documents/Graduate/Mizzou"))))

(use-package counsel-projectile
  :after projectile
  :config
  (counsel-projectile-mode))
#+end_src

** TODO undo tree
#+begin_src emacs-lisp
(use-package undo-tree
  :disabled
  :init
  (global-undo-tree-mode 1))
#+end_src

** Auto-Saving Changed Files
#+begin_src emacs-lisp
(use-package super-save
  :defer 1
  :diminish super-save-mode
  :config
  (super-save-mode +1)
  (setq super-save-auto-save-when-idle t))
#+end_src

** Auto-Reverting Changed Files
#+begin_src emacs-lisp
;; Revert Dired and other buffers
(setq global-auto-revert-non-file-buffers t)

;; Revert buffers when the underlying file has changed
(global-auto-revert-mode 1)
#+end_src

** Flyspell
#+begin_src emacs-lisp
;; check word spelling
(use-package flyspell
  :init
  (progn
    (flyspell-mode 1))
  :config
  (progn 
    (setq ispell-program-name "aspell")
    (setq ispell-list-command "--list") ;; run flyspell with aspell, not ispell
    ))

#+end_src

** Flycheck
#+begin_src emacs-lisp
;; check code syntax
(use-package flycheck
  :hook (prog-mode . flycheck-mode)
  )
#+end_src

** Window management
*** Window history with winner-mode
#+begin_src emacs-lisp
(use-package winner
  :config
  (winner-mode))
#+end_src

*** Window split preference
#+begin_src emacs-lisp
;; set preference to horizontal split
(defun split-window-sensibly-prefer-horizontal (&optional window)
  "Based on split-window-sensibly, but designed to prefer a horizontal split,
i.e. windows tiled side-by-side."
  (interactive)
  (let ((window (or window (selected-window))))
    (or (and (window-splittable-p window t)
             ;; Split window horizontally
             (with-selected-window window
               (split-window-right)))
        (and (window-splittable-p window)
             ;; Split window vertically
             (with-selected-window window
               (split-window-below)))
        (and
         (let ((frame (window-frame window)))
           (or
            (eq window (frame-root-window frame))
            (catch 'done
              (walk-window-tree (lambda (w)
                                  (unless (or (eq w window)
                                              (window-dedicated-p w))
                                    (throw 'done nil)))
                                frame)
              t)))
         (not (window-minibuffer-p window))
         (let ((split-width-threshold 0))
           (when (window-splittable-p window t)
             (with-selected-window window
               (split-window-right))))))))

(setq split-width-threshold  80
      split-height-threshold 30
      ;; xsplit-window-preferred-function 'split-window-sensibly-prefer-horizontal
      )

#+end_src

*** Ace window
#+begin_src emacs-lisp
(use-package ace-window
  ;:bind (("M-o" . ace-window))
  :custom
  (aw-scope 'frame)
  (aw-keys '(?1 ?2 ?3 ?4 ?5 ?6 ?7 ?8 ?9))
  (aw-minibuffer-flag t)
  :config
  (ace-window-display-mode 1))
#+end_src

*** Popper
#+begin_src emacs-lisp
(use-package popper
  :bind (("s-`"   . popper-toggle-latest)
         ("M-`"   . popper-cycle)
         ("s-M-`" . popper-toggle-type))
  :init
  (setq popper-reference-buffers
        '("[Oo]utput\\*$"
          "^\\*Warnings\\*"
          "^\\*Compile-Log\\*"
          "^\\*Messages\\*"
          "^\\*Backtrace\\*"
          "^\\*ielm\\*"
          "^\\*Tex Help\\*"
          "^\\*Shell Command Output\\*"
          "^\\*Async Shell Command\\*"
          "^\\*WordNut\\*"
          "^\\*straight-process\\*"
          help-mode
          eshell-mode
          inferior-ess-r-mode
          inferior-python-mode
          message-mode
          compilation-mode))
  ;; only show the popper in the same project
  ;; (setq popper-group-function #'popper-group-by-project)
  ;; (popper-mode -1)
  (popper-mode +1))
#+end_src

*** TODO Control Buffer Placement

I combine this with =popper.el= now! This is a great feature, as popper turn these buffers to =pop= buffer, so that I could toggle with ~C-`~. Besides =pop= buffer would change my current window placement.
#+begin_src emacs-lisp
(setq display-buffer-base-action
      '(display-buffer-reuse-mode-window
        display-buffer-reuse-window
        display-buffer-same-window))

;; If a popup does happen, don't resize windows to be equal-sized
(setq even-window-sizes nil)

(setq display-buffer-alist
      '(;; top side window
        ("\\*\\(Flymake\\|Package-Lint\\|vc-git :\\).*"
         (display-buffer-in-side-window)
         (window-height . 0.16)
         (side . top)
         (slot . 0))
        ("\\*Messages.*"
         (display-buffer-in-side-window)
         (window-height . 0.16)
         (side . top)
         (slot . 1))
        ("\\*\\(Backtrace\\|Warnings\\|Compile-Log\\)\\*"
         (display-buffer-in-side-window)
         (window-height . 0.16)
         (side . top)
         (slot . 2))
        ;; left side window
        ("\\*Help.*"            ; See the hooks for `visual-line-mode'
         (display-buffer-in-side-window)
         (window-width . 0.5)
         (side . right)
         (slot . -1))
        ;; bottom buffer (NOT side window)
        ("\\*.*\\(e?shell\\|v?term\\).*"
         (display-buffer-reuse-mode-window display-buffer-at-bottom)
         (window-height . 0.35))
        ;; below current window
        ("\\*Calendar.*"
         (display-buffer-reuse-mode-window display-buffer-below-selected)
         (window-height . shrink-window-if-larger-than-buffer))))

;; If a popup does happen, don't resize windows to be equal-sized
(setq even-window-sizes nil)
#+end_src

* Reading & text editing

For reading, I would use =zathura-pdf-mupdf= or =evince=. In particular, =zathura-pdf-mupdf= supports pdf and epub
** special tex mode

#+begin_src emacs-lisp
;; preview markdown
(use-package grip-mode)

;; latex
(use-package tex
  :ensure auctex
  :straight (:type built-in)
  :config
  (setq
   Tex-PDF-mode t
   TeX-parse-self t
   TeX-auto-save t
   Tex-command-show "LaTex"
   TeX-view-program-selection '((output-pdf "Okular"))
   TeX-save-query nil)
  ;; revert the PDF-buffer after the TeX compilation has finished
  (add-hook 'TeX-after-compilation-finished-functions #'TeX-revert-document-buffer)
  (add-hook 'TeX-mode-hook 'flyspell-mode))

(use-package cdlatex
  :bind (:map cdlatex-mode-map
              ("TAB" . indent-for-tab-command)
              ("<C-tab>" . cdlatex-tab)
              ;; turn off auto pairing
              ("$" . nil)
              ("{" . nil)
              ("[" . nil))
  :hook
  (LaTeX-mode . turn-on-cdlatex)
  (latex-mode . turn-on-cdlatex)
  (markdown-mode . turn-on-cdlatex))

;; preview latex
(use-package latex-preview-pane
  :disabled
  :config
  (latex-preview-pane-enable))
#+end_src

** books

#+begin_src emacs-lisp
;; epub
(use-package nov
  :defer 1
  :config
  (add-to-list 'auto-mode-alist '("\\.epub\\'" . nov-mode)))

;; pdf
(use-package pdf-tools
  :disabled
  :pin manual ;; don't reinstall when package updates
  :magic ("%PDF" . pdf-view-mode)
  :config
  (setq-default pdf-view-display-size 'fit-page)
  (setq pdf-annot-activate-created-annotations t)
  (pdf-tools-install :no-query)
  (require 'pdf-occur)
  (setq pdf-view-use-scaling t
        pdf-view-use-imagemagick t)
  )
#+end_src

** dictionary

you need to install the local dictionary =wordnet= (=wordnet-common= in arch). Unfortunately, this package's completing system conflicts with =ivy=..
#+begin_src emacs-lisp
(use-package wordnut)
#+end_src

* Emacs Applications
Don't try =Emacs Applicaiton Framework= untill you solve the overheating issue!!!!!!!!!!!!!!!!!!!!
** Dired
*** main
#+begin_src emacs-lisp
  (use-package dired
    :straight (:type built-in)
    :ensure nil
    :defer 1
    :commands (dired dired-jump)
    :bind (("C-c r" . ranger)
           (:map dired-mode-map
                 ("C-c i" . image-dired)
                 ("C-<return>" . image-dired-dired-display-external)))
    :config
    (setq dired-listing-switches "-agho --group-directories-first"
          dired-omit-files "^\\.[^.].*"
          dired-omit-verbose nil
          dired-hide-details-hide-symlink-targets nil
          delete-by-moving-to-trash t)
    (setq insert-directory-program "ls" dired-use-ls-dired t)        ; sort directories first in dired

    (autoload 'dired-omit-mode "dired-x")

    (add-hook 'dired-load-hook
              (lambda ()
                (interactive)
                (dired-collapse))))
#+end_src

*** dired rainbow
#+begin_src emacs-lisp
(use-package dired-rainbow
  :defer 2
  :config
  (dired-rainbow-define-chmod directory "#6cb2eb" "d.*")
  (dired-rainbow-define html "#eb5286" ("css" "less" "sass" "scss" "htm" "html" "jhtm" "mht" "eml" "mustache" "xhtml"))
  (dired-rainbow-define xml "#f2d024" ("xml" "xsd" "xsl" "xslt" "wsdl" "bib" "json" "msg" "pgn" "rss" "yaml" "yml" "rdata"))
  (dired-rainbow-define document "#9561e2" ("docm" "doc" "docx" "odb" "odt" "pdb" "pdf" "ps" "rtf" "djvu" "epub" "odp" "ppt" "pptx"))
  (dired-rainbow-define markdown "#ffed4a" ("org" "etx" "info" "markdown" "md" "mkd" "nfo" "pod" "rst" "tex" "textfile" "txt"))
  (dired-rainbow-define database "#6574cd" ("xlsx" "xls" "csv" "accdb" "db" "mdb" "sqlite" "nc"))
  (dired-rainbow-define media "#de751f" ("mp3" "mp4" "mkv" "MP3" "MP4" "avi" "mpeg" "mpg" "flv" "ogg" "mov" "mid" "midi" "wav" "aiff" "flac"))
  (dired-rainbow-define image "#f66d9b" ("tiff" "tif" "cdr" "gif" "ico" "jpeg" "jpg" "png" "psd" "eps" "svg"))
  (dired-rainbow-define log "#c17d11" ("log"))
  (dired-rainbow-define shell "#f6993f" ("awk" "bash" "bat" "sed" "sh" "zsh" "vim"))
  (dired-rainbow-define interpreted "#38c172" ("py" "ipynb" "rb" "pl" "t" "msql" "mysql" "pgsql" "sql" "r" "clj" "cljs" "scala" "js"))
  (dired-rainbow-define compiled "#4dc0b5" ("asm" "cl" "lisp" "el" "c" "h" "c++" "h++" "hpp" "hxx" "m" "cc" "cs" "cp" "cpp" "go"
					    "f" "for" "ftn" "f90" "f95" "f03" "f08" "s" "rs" "hi" "hs" "pyc" ".java"))
  (dired-rainbow-define executable "#8cc4ff" ("exe" "msi"))
  (dired-rainbow-define compressed "#51d88a" ("7z" "zip" "bz2" "tgz" "txz" "gz" "xz" "z" "Z" "jar" "war" "ear" "rar" "sar" "xpi" "apk" "xz" "tar"))
  (dired-rainbow-define packaged "#faad63" ("deb" "rpm" "apk" "jad" "jar" "cab" "pak" "pk3" "vdf" "vpk" "bsp"))
  (dired-rainbow-define encrypted "#ffed4a" ("gpg" "pgp" "asc" "bfe" "enc" "signature" "sig" "p12" "pem"))
  (dired-rainbow-define fonts "#6cb2eb" ("afm" "fon" "fnt" "pfb" "pfm" "ttf" "otf"))
  (dired-rainbow-define partition "#e3342f" ("dmg" "iso" "bin" "nrg" "qcow" "toast" "vcd" "vmdk" "bak"))
  (dired-rainbow-define vc "#0074d9" ("git" "gitignore" "gitattributes" "gitmodules"))
  (dired-rainbow-define-chmod executable-unix "#38c172" "-.*x.*"))

#+end_src

*** other packages
#+begin_src emacs-lisp
(use-package dired-single
  :defer 1)

(use-package dired-ranger
  :defer 1)

(use-package dired-collapse
  :defer 1)

(use-package all-the-icons-dired
  :hook
  (dired-mode . all-the-icons-dired-mode))
#+end_src

** Eshell
I disabled show completion history, since it is not very useful and it will mess up with ivy-minibuffer.
Updates Aug 7, 2021: It turns out that binding ~<tab>~ to ~company-complete~ would force to use ivy's buffer. But still, completion history would affect items showing up in company-complete menu.
Updates Aug 7, 2021: I decide to disable company-mode in eshell and use history completion.

#+begin_src emacs-lisp
(defun efs/configure-eshell ()
  ;; Save command history when commands are entered
  (add-hook 'eshell-pre-command-hook 'eshell-save-some-history)

  ;; Truncate buffer for performance
  (add-to-list 'eshell-output-filter-functions 'eshell-truncate-buffer)

  ;; Use completion-at-point to provide completions in eshell
  (define-key eshell-mode-map (kbd "<tab>") 'completion-at-point)
  ;; (define-key eshell-mode-map [remap eshell-pcomplete] 'completion-at-point)
  ;; (define-key eshell-mode-map (kbd "<tab>") 'company-complete)

  (setenv "PAGER" "cat")

  (setq eshell-history-size         10000
        eshell-buffer-maximum-lines 10000
        eshell-hist-ignoredups t
        eshell-scroll-to-bottom-on-input t))

(use-package eshell-git-prompt
  :after eshell)

(use-package eshell
  :hook (eshell-first-time-mode . efs/configure-eshell)
  :config

  (with-eval-after-load 'esh-opt
    (setq eshell-destroy-buffer-when-process-dies t)
    (setq eshell-visual-commands '("htop" "zsh" "vim")))
  )

;; fish completion
(use-package fish-completion
  :hook (eshell-mode . fish-completion-mode))

;; show complete history
(use-package esh-autosuggest
  ;; :disabled
  :hook (eshell-mode . esh-autosuggest-mode)
  :config
  (setq esh-autosuggest-delay 0.5)
  (set-face-foreground 'company-preview-common "#4b5668")
  (set-face-background 'company-preview nil)
  )

;; command highlight
(use-package eshell-syntax-highlighting
  :after esh-mode
  :config
  (eshell-syntax-highlighting-global-mode +1))

(use-package eshell-info-banner
  :disabled
  :straight (eshell-info-banner :type git
                                :host github
                                :repo "phundrak/eshell-info-banner.el")
  :hook (eshell-banner-load . eshell-info-banner-update-banner)
  )

;; themes
(use-package eshell-prompt-extras
  :after esh-mode
  :config
  (with-eval-after-load "esh-opt"
    (autoload 'epe-theme-lambda "eshell-prompt-extras")
    (setq eshell-highlight-prompt t     ; damn! this means ineditable prompt!
          eshell-prompt-function 'epe-theme-lambda))
  )
#+end_src

open an eshell from 
#+begin_src emacs-lisp
(defun zw/show-eshell()
  (interactive)
  ;; (select-window (split-window-vertically -15))
  (eshell)
  ;; (text-scale-set 0.7)
  )
#+end_src

** Org-mode
*** Main
#+begin_src emacs-lisp
(use-package org
  :hook
  (org-mode . org-indent-mode)
  (org-mode . variable-pitch-mode)
  (org-mode . visual-line-mode)
  (org-mode . turn-on-org-cdlatex)
  (org-mode . cdlatex-mode)
  ;; this is defined in "beautify org mode" section
  ;; (org-mode . org-icons)
  ;; refresh image after executing codes
  ;; (org-babel-after-execute . org-redisplay-inline-images)
  :commands (org-capture org-agenda)
  ;; :bind (:map org-mode-map
              ;; ("<C-tab>" . org-latex-preview))
  :config
  (setq ;; other options: "⤵"
   org-ellipsis " ▾"
   org-hide-emphasis-markers t
   org-src-fontify-natively t
   org-fontify-quote-and-verse-blocks t
   org-edit-src-content-indentation 2
   org-hide-block-startup nil
   ;; preview latex, etc at startup
   org-pretty-entities nil
   org-startup-folded  t;;'content
   org-cycle-separator-lines 2
   org-confirm-babel-evaluate nil
   org-src-preserve-indentation t    ; helps to indent python code in org mode
   org-src-tab-acts-natively t
   org-src-strip-leading-and-trailing-blank-lines t
   ;; show edit buffer below the current window, keeping all
   org-src-window-setup 'split-window-below
   ;; use user defined image size
   org-image-actual-width nil
   ;; make latex formula larger
   org-format-latex-options (plist-put org-format-latex-options :scale 3.4))
  (setq org-todo-keyword-faces
	'(("TODO" . (:foreground "orange red" :weight bold))
	  ("DONE" . (:foreground "green" :weight bold))))

  ;; set org babel languages
  (with-eval-after-load 'org
    (org-babel-do-load-languages
     'org-babel-load-languages
     '((python . t)
       ;; (ipython . t)
       (R . t)
       (shell . t)
       (ein . t)
       (lisp . t)
       (latex . t)
       (teximg . t)))

    ;; This is needed as of Org 9.2 (use yasnippet instead)
    (require 'org-tempo)
    (add-to-list 'org-structure-template-alist '("sh" . "src sh"))
    (add-to-list 'org-structure-template-alist '("el" . "src emacs-lisp"))
    (add-to-list 'org-structure-template-alist '("py" . "src python :session"))
    (add-to-list 'org-structure-template-alist '("pyo" . "src python :session :results output"))
    (add-to-list 'org-structure-template-alist '("pyp" . "src python :session :results file")))
  :config
  ;; latex
  (setq org-highlight-latex-and-related '(latex script entities))
  ;; export to latex
  (require 'ox-latex)
  (add-to-list 'org-latex-packages-alist '("" "minted"))
  (setq org-latex-listings 'minted)

  (setq org-latex-pdf-process
        '("pdflatex -shell-escape -interaction nonstopmode -output-directory %o %f"
          "pdflatex -shell-escape -interaction nonstopmode -output-directory %o %f"
          "pdflatex -shell-escape -interaction nonstopmode -output-directory %o %f"))

  (with-eval-after-load 'ox-latex
    (add-to-list 'org-latex-classes
                 '("org-plain-latex"
                   "\\documentclass{article}
           [NO-DEFAULT-PACKAGES]
           [PACKAGES]
           [EXTRA]"
                   ("\\section{%s}" . "\\section*{%s}")
                   ("\\subsection{%s}" . "\\subsection*{%s}")
                   ("\\subsubsection{%s}" . "\\subsubsection*{%s}")
                   ("\\paragraph{%s}" . "\\paragraph*{%s}")
                   ("\\subparagraph{%s}" . "\\subparagraph*{%s}"))))
  )
#+end_src

*** Org-fragtog
#+begin_src emacs-lisp
(use-package org-fragtog
  :hook
  (org-mode . org-fragtog-mode))
#+end_src

*** Org agenda
**** main
#+begin_src emacs-lisp
(setq org-agenda-window-setup 'current-window)
(setq org-agenda-start-with-log-mode t)
(setq org-agenda-span 'day)
(setq org-log-into-drawer t)
;; Make done tasks show up in the agenda log
(setq org-log-done 'time)
(setq org-log-into-drawer t)

(setq org-agenda-files
      '("~/Workspace/Documents/OrgFiles/Tasks.org"
        "~/Workspace/Documents/OrgFiles/Events.org"))

;; refiling
(setq org-refile-targets
      '(("Tasks.org" :maxlevel . 1)
        ("Events.org" :maxlevel . 1)))

;; Save Org buffers after refiling!
(advice-add 'org-refile :after 'org-save-all-org-buffers)
#+end_src

**** org-capture
#+begin_src emacs-lisp
(setq org-capture-templates
      `(("t" "Tasks / Projects")
        ("tt" "Task" entry (file+olp "~/Workspace/Documents/OrgFiles/Tasks.org" "Inbox")
         "* TODO %?\n  %U\n  %a\n  %i" :empty-lines 1)
        ("tr" "Research" entry (file+olp "~/Workspace/Documents/OrgFiles/Tasks.org" "Research")
         "* TODO %?\n  %U\n  %a\n  %i" :empty-lines 1)
        ("ts" "Clocked Entry Subtask" entry (clock)
         "* TODO %?\n  %U\n  %a\n  %i" :empty-lines 1)

        ("e" "Events")
        ("em" "Meeting" entry
         (file+olp+datetree "~/Workspace/Documents/OrgFiles/Events.org", "Meeting")
         "* %<%I:%M %p> - %a :meetings:\n\n%?\n\n"
         :clock-in :clock-resume
         :empty-lines 1)))
#+end_src

**** notification
org alert for sending notifications
#+begin_src emacs-lisp
(use-package org-wild-notifier
  :hook (after-init . org-wild-notifier-mode)
  :config
  (setq org-wild-notifier-alert-time '(15))
  (setq org-wild-notifier-notification-title "Org Agenda")
  (setq org-wild-notifier--alert-severity 'high)
  (setq org-wild-notifier--day-wide-events t))

(use-package alert
  :config
  (setq alert-default-style 'libnotify))
#+end_src

*** Beautify org mode
**** by packages

#+begin_src emacs-lisp
(use-package org-superstar
  :after org
  :hook (org-mode . org-superstar-mode)
  :custom
  (org-superstar-remove-leading-stars nil)
  ;; (org-superstar-headline-bullets-list '("◉" "○" "●" "○" "●" "○" "●"))
  )


(defun efs/org-mode-visual-fill ()
  (setq visual-fill-column-width 100
        visual-fill-column-center-text t)
  (visual-fill-column-mode 1))

(use-package visual-fill-column
  :hook (org-mode . efs/org-mode-visual-fill))

;; Auto-show Markup Symbols
(use-package org-appear
  :hook (org-mode . org-appear-mode))

;; auto tangle
(use-package org-auto-tangle
  ;; :load-path "site-lisp/org-auto-tangle/"    ;; this line is necessary only if you cloned the repo in your site-lisp directory
  :defer 1
  :hook (org-mode . org-auto-tangle-mode))
#+end_src

**** by setting

#+begin_src emacs-lisp
;; Replace list hyphen with dot
(font-lock-add-keywords 'org-mode
                        '(("^ *\\([-]\\) "
                           (0 (prog1 () (compose-region (match-beginning 1) (match-end 1) "•"))))))

(let* (
       (variable-tuple '(:font "JetBrainsMono Nerd Font"))
       (base-font-color     (face-foreground 'default nil 'default))
       (headline           `(:inherit default :weight bold :foreground ,base-font-color)))

  (custom-theme-set-faces
   'user
   `(org-level-8 ((t (,@headline ,@variable-tuple))))
   `(org-level-7 ((t (,@headline ,@variable-tuple))))
   `(org-level-6 ((t (,@headline ,@variable-tuple))))
   `(org-level-5 ((t (,@headline ,@variable-tuple))))
   `(org-level-4 ((t (,@headline ,@variable-tuple :height 1.1 :italic t :foreground "#FF8C94" :slant italic))))
   `(org-level-3 ((t (,@headline ,@variable-tuple :height 1.25 :foreground "#D08770"))))
   `(org-level-2 ((t (,@headline ,@variable-tuple :height 1.50 :foreground "#88C0D0"))))
   `(org-level-1 ((t (,@headline ,@variable-tuple :height 1.75 :foreground "#5E81AC"))))
   `(org-document-title ((t (,@headline ,@variable-tuple :height 2.0 :underline t))))))

(custom-theme-set-faces
   'user
   '(org-block ((t (:inherit fixed-pitch))))
   '(org-code ((t (:inherit (shadow fixed-pitch)))))
   '(org-document-info ((t (:foreground "dark orange"))))
   '(org-document-info-keyword ((t (:inherit (shadow fixed-pitch)))))
   '(org-indent ((t (:inherit (org-hide fixed-pitch)))))
   '(org-link ((t (:foreground "royal blue" :underline t))))
   '(org-meta-line ((t (:inherit (font-lock-comment-face fixed-pitch)))))
   '(org-property-value ((t (:inherit fixed-pitch))) t)
   '(org-special-keyword ((t (:inherit (font-lock-comment-face fixed-pitch)))))
   '(org-table ((t (:inherit fixed-pitch :foreground "#83a598"))))
   '(org-tag ((t (:inherit (shadow fixed-pitch) :weight bold :height 0.8))))
   '(org-verbatim ((t (:inherit (shadow fixed-pitch)))))
   '(org-latex-and-related ((t (:foreground "#EBCB8B"))))

   '(org-block-begin-line ((t (:background "#4C566A" :foreground "#ECEFF4"
                                                :bold t :height 1.0))))
   '(org-block-end-line ((t (:background "#4C566A" :foreground "#ECEFF4"
                                              :bold t :height 1.0)))))

(defun org-icons ()
   "Beautify org mode keywords."
   (setq prettify-symbols-alist '(("[ ]" . "")
			          ("[X]" . "")
			          ("[-]" . "")
			          ("#+BEGIN_SRC" . "")
			          ("#+END_SRC" . "―")
                                  ("#+begin_src" . "")
			          ("#+end_src" . "―")
                                  ("#+results:" . "")
                                  ("#+RESULTS:" . "")))
   (prettify-symbols-mode))



;; (setq  org-src-block-faces '(("emacs-lisp" (:background "LightCyan1" :extend t))
;; 			     ("python" (:background "DarkSeaGreen1" :extend t))
;; 			     ("R" (:background "thistle1" :extend t))))
#+end_src

*** Org-roam
#+begin_src emacs-lisp
(use-package org-roam
  :after org
  :init
  (setq org-roam-v2-ack t)
  :custom
  (org-roam-directory "~/Workspace/Documents/RoamNotes")
  (org-roam-completion-everywhere t)
  (org-roam-capture-templates
   '(
     ;; default template
     ("d" "default" plain
      "%?"
      :if-new (file+head "%<%Y%m%d%H%M%S>-${slug}.org" "#+title: ${title}\n")
      :unnarrowed t)
     ;; few example templates
     ("l" "programming language" plain
      "* Characteristics\n\n- Family: %?\n- Inspired by: \n\n* Reference:\n\n"
      :if-new (file+head "%<%Y%m%d%H%M%S>-${slug}.org" "#+title: ${title}\n")
      :unnarrowed t)

     ("b" "book notes" plain
      "\n* Source\n\nAuthor: %^{Author}\nTitle: ${title}\nYear: %^{Year}\n\n* Summary\n\n%?"
      :if-new (file+head "%<%Y%m%d%H%M%S>-${slug}.org" "#+title: ${title}\n")
      :unnarrowed t)

     ("p" "project" plain "* Goals\n\n%?\n\n* Tasks\n\n** TODO Add initial tasks\n\n* Dates\n\n"
      :if-new (file+head "%<%Y%m%d%H%M%S>-${slug}.org" "#+title: ${title}\n#+filetags: Project")
      :unnarrowed t)
     ))
  :bind (("C-c n l" . org-roam-buffer-toggle)
         ("C-c n f" . org-roam-node-find)
         ("C-c n i" . org-roam-node-insert)
         :map org-mode-map
         ("C-M-i"   . completion-at-point))
  :config
  (org-roam-setup))
#+end_src

*** Org-present
A simple presentation for org mode. I stole this from [[https://github.com/daviwil/dotfiles/blob/master/Emacs.org][Daviwil's config]].
#+begin_src emacs-lisp
(defun dw/org-present-prepare-slide ()
  (org-overview)
  (org-show-entry)
  (org-show-children))

(defun dw/org-present-hook ()
  (setq-local face-remapping-alist '((default (:height 1.5) variable-pitch)
                                     (header-line (:height 4.5) variable-pitch)
                                     (org-document-title (:height 1.75) org-document-title)
                                     (org-code (:height 1.55) org-code)
                                     (org-verbatim (:height 1.55) org-verbatim)
                                     (org-block (:height 1.25) org-block)
                                     (org-block-begin-line (:height 0.7) org-block)))
  (setq header-line-format " ")
  (org-appear-mode -1)
  (org-display-inline-images)
  (dw/org-present-prepare-slide))

(defun dw/org-present-quit-hook ()
  (setq-local face-remapping-alist '((default variable-pitch default)))
  (setq header-line-format nil)
  (org-present-small)
  (org-remove-inline-images)
  (org-appear-mode 1))

(defun dw/org-present-prev ()
  (interactive)
  (org-present-prev)
  (dw/org-present-prepare-slide))

(defun dw/org-present-next ()
  (interactive)
  (org-present-next)
  (dw/org-present-prepare-slide))

(use-package org-present
  :bind (:map org-present-mode-keymap
         ("C-c n" . dw/org-present-next)
         ("C-c p" . dw/org-present-prev)
         ("C-c q" . org-present-quit)
         ;; ([C-right] . org-present-next)
         ;; ([C-left] . org-present-prev)
         ([C-right] . dw/org-present-next)
         ([C-left] . dw/org-present-prev)
         ([right] . nil)
         ([left] . nil))
  :hook ((org-present-mode . dw/org-present-hook)
         (org-present-mode-quit . dw/org-present-quit-hook)))
#+end_src

*** Custom functions
#+begin_src emacs-lisp
(defun zw/org-fold-all-but-current ()
  (interactive)
  (org-remove-occur-highlights)
  (org-overview)
  (org-reveal))

(defun zw/toggle-image-scroll ()
  (interactive)
  (pixel-scroll-mode)
  )

;; teximg
(require 'ob-teximg)
#+end_src

** Games

* Desktop (EXWM)
This part is largely copied from daviwil's course.

#+begin_src emacs-lisp
(when (eq system-type 'gnu/linux)
  (org-babel-load-file "~/.emacs.d/emacs-desktop.org"))
#+end_src

* Development

#+begin_src emacs-lisp
(org-babel-load-file "~/.emacs.d/emacs-development.org")
#+end_src

* External system

#+begin_src emacs-lisp
(when (eq system-type 'gnu/linux)
  (org-babel-load-file "~/.emacs.d/emacs-system.org"))
#+end_src

* Key-bindings

key bindings in minibuffer will over the ones in major buffer. For example, in EXWM I defined ~s-<tab>~ to be ~switch-to-buffer~. Then, in the ivy's minibuffer, I overwrite ~s-<tab>~ to be navigate through the items in ivy.

My key binding strategy:
+ super key is used for exwm/frame/window/buffer level commands
+ super key is also used for some MacOS keys
+ ctrl key is used for tab level commands
+ ctrl+x prefix is used for emacs' default commands
+ ctrl+c prefix is used for custom commands

#+begin_src emacs-lisp
;; Make ESC quit prompts
(global-set-key (kbd "<escape>") 'keyboard-escape-quit)
;; undo
(global-set-key (kbd "s-z") 'undo)
;; cut copy paste
(global-set-key (kbd "s-x") 'kill-region)
(global-set-key (kbd "s-c") 'kill-ring-save)
(global-set-key (kbd "s-v") 'yank)
;; window
(global-set-key (kbd "s-w") 'delete-window)
(global-set-key (kbd "s-t") 'split-window-sensibly-prefer-horizontal)

;; check dict
(global-set-key (kbd "C-c w") 'wordnut-search)
(global-set-key (kbd "C-c W") 'wordnut-lookup-current-word)

;; toggle transparency
(global-set-key (kbd "C-c t") 'zw/toggle-transparency)

;; get passwed
;; (global-set-key (kbd "C-c p") 'zw/get-passwd)

;; toggle input
(global-set-key (kbd "C-\\") 'toggle-input-method)
(bind-key "C-`" 'rime-send-keybinding rime-mode-map)
#+end_src
